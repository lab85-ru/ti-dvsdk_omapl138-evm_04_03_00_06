;
;  Copyright 2010 by Texas Instruments Incorporated.
;  @(#) DSP/BIOS_Kernel 5,2,5,28 02-10-2010 (cuda-u28)
;
;
;  ======== dio.h28 ========
;
;

        .if ($isdefed("DIO_") = 0) ; prevent multiple includes of this file
DIO_    .set    1

        .include std.h28
        .include cinit.h28
        .include dev.h28
        .include sem.h28
        .include que.h28

dioObjAligned           .set    2       ; dioObjAligned may have CodePtr element
dioParamsAligned        .set    1


DIO_Params              .struct
name            DataPtr 1
chanParams      DataPtr 1
DIO_A_PARAMSSIZE        .endstruct

        .eval DIO_A_PARAMSSIZE, DIO_IPARSIZE

;
;  This struct contains the larger part of the union of DIO_Obj in dio.h.
;  Since there is no way to express the union, the larger part of the union
;  is use to allocate sufficient memory to handle all cases.
;
DIO_Obj .struct
fxns            DataPtr 1
hChan           DataPtr 1
cbFxn           CodePtr 1
arg0            Arg     1
arg1            Arg     1
DIO_A_OBJSIZE   .endstruct
        
        .eval   DIO_A_OBJSIZE, DIO_OBJSIZE

;
;# ======== DIO_Obj ========
;
;#
;# Preconditions:
;#      none
;#
;# Postconditions:
;#      none
;#
;
        .asg    "", DIO_Obj$regs
DIO_Obj .macro cflag, name, id, _fxnsTable, _initFxn, _device, _chanParams

        .sect   ".econst"
DIO_:name:      .string "/:name:",0     ; Set up string "/<devName>"

:name:$ctrl     .string "/:_device:", 0 ; Control String

        ;
        ;  Allocate and initialize DIO_Params object.
        ;  Create symbol SIO_:name:$params for this object.
        ;  The params field in DEV_Obj will be set to SIO_:name:$params.
        ;  This is done in SIO_Obj.
        ;
        C28_allocateObject dioParamsAligned, SIO_:name:$params, DIO_IPARSIZE, ".dio", 0
        C28_cinitHeader CINITALIGN, dioParamsAligned, SIO_:name:$params, DIO_IPARSIZE
        C28_cinitBegin  dioParamsAligned
        C28_cinitDataPtr :name:$ctrl            ; name
        C28_cinitDataPtr :_chanParams:          ; chanParams
        C28_cinitEnd    dioParamsAligned

        ;
        ; This adds an entry to the device table, DEV_devtab, and the init
        ; table, DEV_devinit.
        ;
        .global :_fxnsTable:
        DEV_addDevice DIO_:name:, :_fxnsTable:, 0, SIO_:name:$params, :_initFxn:, DEV_SIOTYPE, 0

        .endm

;
;# ======== DIO_config ========
;  Static configuration of the DIO module
;
;#
;# Preconditions:
;#      none
;#
;# Postconditions:
;#      none
;#
;
        .asg    "", DIO_config$regs
DIO_config      .macro _static, _numTskBase, _numSwiBase
        .if (:_static:)
            .if (:_numTskBase: != 0)
                .global _DIO_NUMTSKSTATIC  ; Number of Static TSK DIO objects 
                .global _DIO_TSKTABLE      ; Table of DIO objects for TSK.

                .eval   DIO_OBJSIZE * :_numTskBase:, DIO_TABLESIZE

                ;
                ;  Allocate and initialize the DIO_NUMTSKSTATIC value.
                ;  This value is used in Static TSK version of DIO.
                ;
                C28_allocateObject 1, _DIO_NUMTSKSTATIC, 1, ".dio", 0
                C28_cinitHeader  CINITALIGN, 1, _DIO_NUMTSKSTATIC, (1 * STD_TARGWORDMAUS)
                C28_cinitBegin  1
                C28_cinitInt    :_numTskBase:
                C28_cinitEnd    1

                ;
                ;  Allocate the TSK DIO table
                ;
                C28_allocateObject dioObjAligned, _DIO_TSKTABLE, DIO_TABLESIZE, ".dio", 0
                .eval 0, kount

                .loop

                    ;
                    ;  Initialize the TSK DIO table.
                    ;  Note:  This initialization does not match the DIO_Obj
                    ;         struct above because this is the other fields
                    ;         in the union. Its vital that this matches the
                    ;         DIO_Obj in dio.h
                    ;
                    C28_cinitHeader  CINITALIGN, dioObjAligned, _DIO_TSKTABLE + DIO_OBJSIZE * :kount:, DIO_OBJSIZE
                    C28_cinitBegin      dioObjAligned
                    C28_cinitDataPtr    0                       ; fxns
                    C28_cinitDataPtr    0                       ; chanp
                    C28_cinitDataPtr    dio:kount:$complete     ; complete
                    C28_cinitDataPtr    0                       ; ready
                    C28_cinitLong       0       ; extra word for union
                        
                    C28_cinitEnd        dioObjAligned

                    ; create the complete SEM object
                    DIO_semObject dio:kount:$complete, 0

                    .eval kount + 1, kount
                    .break (kount == :_numTskBase:)
                .endloop
            .endif      ; .if (:_numTskBase: != 0)

            .if (:_numSwiBase: != 0)
                .global _DIO_NUMCBSTATIC  ; Number of Cb Static DIO objects.
                .global _DIO_CBTABLE      ; Table of DIO objects for Callback.

                .eval DIO_OBJSIZE * :_numSwiBase:, DIO_TABLESIZE

                ;
                ;  Allocate and initialize the DIO_NUMCBSTATIC value
                ;  This value is used in Static Callback version of DIO.
                ;
                C28_allocateObject 1, _DIO_NUMCBSTATIC, 1, ".dio", 0
                C28_cinitHeader  CINITALIGN, 1, _DIO_NUMCBSTATIC, (1 * STD_TARGWORDMAUS)
                C28_cinitBegin  1
                C28_cinitInt    :_numSwiBase:  
                C28_cinitEnd    1 

                ;
                ;  Allocate the Callback DIO Table
                ;
                C28_allocateObject dioObjAligned, _DIO_CBTABLE, DIO_TABLESIZE, ".dio", 0
                .eval 0, kount

                .loop

                    ;
                    ;  Initialize the Callback DIO Table
                    ;
                    C28_cinitHeader  CINITALIGN, dioObjAligned, _DIO_CBTABLE + DIO_OBJSIZE * :kount:, DIO_OBJSIZE
                    C28_cinitBegin      dioObjAligned
                    C28_cinitDataPtr    0               ; fxns
                    C28_cinitDataPtr    0               ; chanp
                    C28_cinitCodePtr    0               ; context.cb.fxn
                    C28_cinitArg        0               ; context.cb.arg0
                    C28_cinitArg        0               ; context.cb.arg1
                    C28_cinitEnd        dioObjAligned
                    
                    .eval kount + 1, kount
                    .break (kount == :_numSwiBase:)
                .endloop
            .endif      ; .if (:_numSwiBase: != 0)
        .endif          ; .if (:_static:)

        .endm

;
;# ======== DIO_end ========
;  Invoked at the end of all other configuration declarations.
;
;#
;# Preconditions:
;#      none
;#
;# Postconditions:
;#      none
;#
;
        .asg    "", DIO_end$regs
DIO_end .macro
        .endm

;
;# ======== DIO_init ========
;  Runtime initialization of the DIO module
;
;#
;# Preconditions: 
;#      none
;#
;# Postconditions:
;#      none
;#
;
        .asg    "", DIO_init$regs
DIO_init        .macro
        .endm

;
;# ======== DIO_startup ========
;
;#
;# Preconditions:
;#      none
;#
;# Postconditions:
;#      none
;#
        .asg    "", DIO_startup$regs
DIO_startup        .macro
        .endm

;
;# ======== DIO_semObject ========
;  Use to create SEM objects for DIO objects using TSK
;#
;# Preconditions:
;#      none
;#
;# Postconditions:
;#      none
;#
;
        .asg    "", SEM_Obj$regs
DIO_semObject .macro name, _count

        .global :name:

        ; allocate space for sem.

        C28_allocateObject   isSemAligned, :name:, SEM_CORESIZE , ".dio", 0

        C28_cinitHeader CINITALIGN, isSemAligned, :name:, SEM_CORESIZE

        SEM_cinitObj  :name:, :_count:

        .endm

        .endif          ; if DIO_ is not defined
